plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.firebase.crashlytics'
    id 'kotlinx-serialization'
}


android {
    compileSdkVersion target_sdk_version
    defaultConfig {
        applicationId "org.peercast.core"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode version_code
        versionName version_name
        buildConfigField("String", "YT_VERSION", "\"${yt_version}\"")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        //ndkVersion "23.1.7779620"

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_shared"
                //version "3.18.1"
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/jni/CMakeLists.txt"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            firebaseCrashlytics {
                nativeSymbolUploadEnabled true
            }
        }
    }

    buildFeatures {
        dataBinding = true
        prefab = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/LICENSE.txt', 'META-INF/NOTICE.txt', 'META-INF/beans.xml']
        }
    }


    lint {
        abortOnError false
        checkReleaseBuilds false
    }
    namespace 'org.peercast.core'
}


task assetZipTask(type:Exec) {
    println "create assets/peca-yt.zip"
    workingDir 'src/main/jni'
    executable = 'bash'
    args = ["./generate-htmlzip.sh"]
}
tasks.preBuild.dependsOn assetZipTask


dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-core:$serialization_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$serialization_version"

    implementation "androidx.appcompat:appcompat:$androidx_compat_version"
    implementation "androidx.core:core-ktx:$androidx_core_version"

    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-service:$lifecycle_version"

    implementation platform('com.google.firebase:firebase-bom:28.1.0')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-crashlytics-ndk'

    implementation "io.insert-koin:koin-android:$koin_version"

    implementation "com.jakewharton.timber:timber:$timber_version"

    implementation "androidx.work:work-runtime:$work_version"
    implementation "androidx.work:work-runtime-ktx:$work_version"

    implementation "com.android.ndk.thirdparty:openssl:1.1.1l-beta-1"

    implementation project(':libpeercast')
    implementation project(':common')
    implementation project(':ui')

    implementation fileTree(dir: 'libs', include: ['*.jar'])


    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.json:json:20180813'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
}

apply plugin: 'com.google.gms.google-services'
