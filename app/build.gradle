apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'


android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "org.peercast.core"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 2999000
        versionName "3.0.0-rc00"
        buildConfigField("String", "YT_VERSION", "\"YT28A\"")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    externalNativeBuild {
        cmake {
            path "src/main/jni/CMakeLists.txt"
        }
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}


android {
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}


android {
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/beans.xml'
    }
}

repositories {
    maven { url 'http://4thline.org/m2' }
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1"

    def androidx_version = "1.1.0-alpha04"
    implementation "androidx.appcompat:appcompat:$androidx_version"
    implementation "androidx.core:core-ktx:$androidx_version"
    implementation "androidx.browser:browser:1.0.0"

    implementation "androidx.preference:preference:1.0.0"
    implementation "com.google.android.material:material:1.0.0"

    implementation 'org.jsoup:jsoup:1.11.3'

    def lifecycle_version = "2.1.0-alpha04"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"


    def koin_version = '2.0.0-beta-1'
    implementation "org.koin:koin-core:$koin_version"
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"

    implementation 'com.jakewharton.timber:timber:4.7.0'

    implementation project(':pecaport')
    implementation project(':libpeercast')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  //  implementation "com.thetransactioncompany:jsonrpc2-client:1.16.4"

    implementation fileTree(dir: 'libs', include: ['*.jar'])


    testImplementation 'junit:junit:4.12'
    testImplementation 'org.json:json:20180130'
    androidTestImplementation 'androidx.test:runner:1.1.2-alpha01'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.2-alpha01'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
}


task generateAssetHtml(type: Exec) {
    workingDir 'src/main/jni/peercast-yt/ui'
    executable "ruby"
    args "generate-html"
}

task createAssetHtmlZip(type: Zip, dependsOn: 'generateAssetHtml') {
    from 'src/main/jni/peercast-yt/ui/'
    include "html/"
    from 'src/main/jni/peercast-yt/ui/linux'
    include "assets/"
    archiveFileName.set('peca-yt.zip')
    destinationDirectory.set(new File(projectDir, "src/main/assets/"))
}

